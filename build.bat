@ECHO OFF
SETLOCAL

CALL :NORMALIZEPATH "C:\dev-tools\bin\msvc-no-vcvars"
PUSHD %RETVAL%
  CALL "setup_cl_generic.bat" x64
POPD

SET NO_OUTPUT=^>nul 2^>nul
SET PROJECT_ALIAS=chimmy

SET PREPROCESSOR=
SET EXCEPTIONS=/GR- /EHa-
SET WARNINGS=/WX /W4

SET STANDARD=/std:c11
SET SUBSYSTEM=/SUBSYSTEM:Console

SET COMMON_COMPILER_FLAGS=/nologo %PREPROCESSOR% %STANDARD% ^
                          %EXCEPTIONS% %SECURITY% %WARNINGS% /FC
REM Clear indentation and trailing whitespace.
SET COMMON_COMPILER_FLAGS=%COMMON_COMPILER_FLAGS: =%
SET COMMON_COMPILER_FLAGS=%COMMON_COMPILER_FLAGS:/= /%
SET COMMON_COMPILER_FLAGS=%COMMON_COMPILER_FLAGS:*/=/%

SET COMMON_LINKER_FLAGS=/INCREMENTAL:NO /OPT:REF %SUBSYSTEM% ^
                        /ENTRY:mainCRTStartup /NODEFAULTLIB:libcmt.lib
REM Clear indentation and trailing whitespace.
SET COMMON_LINKER_FLAGS=%COMMON_LINKER_FLAGS: =%
SET COMMON_LINKER_FLAGS=%COMMON_LINKER_FLAGS:/= /%
SET COMMON_LINKER_FLAGS=%COMMON_LINKER_FLAGS:*/=/%

SET OPTIMIZATION_MODE=/O2

SET INTERNAL_LIBS=shell32.lib user32.lib gdi32.lib winmm.lib

SET EXTERNAL_INCLUDE_PATHS=/I"C:\Program Files\raylib-4.0.0\include"
SET EXTERNAL_LIB_PATHS=/LIBPATH:"libs"
SET EXTERNAL_LIBS=raylib.lib

SET BUILD_COMMAND=^
%COMMON_COMPILER_FLAGS% %OPTIMIZATION_MODE% ^
%EXTERNAL_INCLUDE_PATHS% /Z7 "%PROJECT_ALIAS%.c" ^
%INTERNAL_LIBS% /link %COMMON_LINKER_FLAGS% ^
%EXTERNAL_LIB_PATHS% %EXTERNAL_LIBS%

CL %BUILD_COMMAND%

ENDLOCAL

:: ========== FUNCTIONS ==========
EXIT /B

:NORMALIZEPATH
  SET RETVAL=%~f1
  GOTO :EOF
